buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'

    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.2'
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        applicationId 'com.moviepass.mobile'
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 143
        versionName "3.1.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        buildConfigField "String", "baseUrl", '"https://staging.moviepass.com/"'
        buildConfigField "String", "STATIC_URL", '"https://a1.moviepass.com/staging/"'
        buildConfigField "String", "registrationURL", '"https://registration-stg.herokuapp.com"'
        buildConfigField "String", "microServiceURL", '"https://authorization-service-stg.herokuapp.com/"'
        buildConfigField "String", "COGNITO_KEY", '"us-east-1:4b4b52ee-11c2-4c56-b109-b654a1d2ad37"'
        buildConfigField "String", "GO_WATCH_IT_URL", '"https://click.moviepass.com/"'
        buildConfigField "String", "REGISTRATION_URL", '"https://stg.moviepass.com"'
        buildConfigField "String", "DEFAULT_LOCATION", 'null'
        buildConfigField "String", "REACTIVATION_URL", '"https://stg.moviepass.com/reactivate"'
        buildConfigField "com.mobile.location.UserLocation", "USER_LOCATION", 'null'
    }

    splits {
        density {
            exclude "ldpi"
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    signingConfigs {
        release {
            keyAlias 'upload'
            keyPassword 'moviepass'
            storeFile file("upload_key.jks")
            storePassword 'moviepass'
        }
        debug {
            storeFile file('../app_listing/keys/debug.keystore')
        }
    }

    flavorDimensions 'env'

    productFlavors {

        staging {
            dimension 'env'
            applicationIdSuffix '.staging'
            buildConfigField "String", "ENVIRONMENT", '"staging"'
            buildConfigField "String", "BUCKET", '"mpcore.staging.tickets"'
            buildConfigField "String", "TOKENIZATION_KEY", '"sandbox_snpbh3w8_2fj37y3ndsjstzxv"'
            resValue 'string', 'authority_file_provider', "\"${defaultConfig.applicationId}.debug.fileprovider\""
            resValue 'string', 'authority_file_provider_path', "\"Android/data/${defaultConfig.applicationId}.debug/files/Pictures\""
            ext.betaDistributionGroupAliases = "moviepass-group"
            ext.betaDistributionReleaseNotesFilePath = "release-notes.txt"
        }

        prod {
            dimension 'env'
            resValue 'string', 'authority_file_provider', "\"${defaultConfig.applicationId}.fileprovider\""
            resValue 'string', 'authority_file_provider_path', "\"Android/data/${defaultConfig.applicationId}/files/Pictures\""
            buildConfigField "String", "ENVIRONMENT", '"prod"'
            buildConfigField "String", "BUCKET", '"mpcore.production.tickets"'
            buildConfigField "String", "TOKENIZATION_KEY", '"sandbox_snpbh3w8_2fj37y3ndsjstzxv"'
            buildConfigField "String", "baseUrl", '"https://api.moviepass.com/"'
            buildConfigField "String", "STATIC_URL", '"https://a1.moviepass.com/prod/"'
            buildConfigField "String", "registrationURL", '"https://registration.moviepass.com/"'
            buildConfigField "String", "microServiceURL", '"https://auth.moviepass.com/"'
            buildConfigField "String", "REGISTRATION_URL", '"https://www.moviepass.com"'
            buildConfigField "String", "REACTIVATION_URL", '"https://www.moviepass.com/reactivate"'
            ext.betaDistributionGroupAliases = "moviepass-group"
            ext.betaDistributionReleaseNotesFilePath = "release-notes.txt"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "String", "DEFAULT_LOCATION", '"arlington tx"'
            buildConfigField "com.mobile.location.UserLocation", "USER_LOCATION", "new com.mobile.location.UserLocation(33.027907,-96.789089)"
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

//    /** THIS FIXES LOLLIPOP DEVICES DO NOT DELETE */
    dexOptions {
        preDexLibraries false
        javaMaxHeapSize "4G"
    }


    defaultConfig {
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath false
            }
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}
repositories {
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.google.com' }
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://github.com/taplytics/Taplytics-Android-SDK/raw/master/AndroidStudio/" }
    google()
}

ext {
    dagger_version = '2.16'
    play_services_version = '15.0.1'
    retrofit_version = '2.3.0'
    exoplayer_version = '2.8.4'
    support_lib_version = '27.1.1'
    firebase_version = "16.0.1"
}

androidExtensions {
    experimental = true
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.google.firebase:firebase-messaging:17.3.0'
    implementation 'com.android.support:support-v4:27.1.1'
    debugImplementation('io.socket:socket.io-client:+') {
        exclude group: 'org.json', module: 'json'
    }
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Play Services
    implementation "com.google.android.gms:play-services-location:$play_services_version"
    implementation "com.google.android.gms:play-services-maps:$play_services_version"
    implementation "com.google.android.gms:play-services-places:$play_services_version"
    implementation "com.google.android.gms:play-services-vision:$play_services_version"
    implementation("com.google.android.gms:play-services-gcm:$play_services_version")

    //Support Libraries
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // Google Maps Utilities
    implementation 'com.google.maps.android:android-maps-utils:0.5'
    // Firebase
    implementation "com.google.firebase:firebase-appindexing:$firebase_version"
    implementation "com.google.firebase:firebase-crash:$firebase_version"
    implementation "com.google.firebase:firebase-messaging:$firebase_version"
    // Dagger
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"
    implementation "com.google.dagger:dagger:$dagger_version"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    implementation 'com.jaeger.statusbarutil:library:1.5.1'

    //AWS
    implementation 'com.amazonaws:aws-android-sdk-core:2.+'
    implementation 'com.amazonaws:aws-android-sdk-s3:2.6.+'
    implementation 'com.amazonaws:aws-android-sdk-cognito:2.6.+'
    implementation 'com.amazonaws:aws-android-sdk-ddb:2.6.+'
    implementation('com.amazonaws:aws-android-sdk-mobile-client:2.6.7@aar') { transitive = true; }

    // Braintree
    implementation('com.braintreepayments.api:drop-in:3.+') {
        exclude group: 'com.android.support'
    }
    // Butterknife
    implementation 'com.jakewharton:butterknife:8.8.1'
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'
    // CardIO
    implementation 'io.card:android-sdk:5.5.1'
    // Custom Shape Image View
    implementation 'com.mostafagazar:customshapeimageview:1.0.4'
    // EventBus
    implementation 'de.greenrobot:eventbus:2.4.0'
    // FAB
    implementation 'com.github.clans:fab:1.5.0'
    // Facebook
    implementation("com.facebook.android:facebook-android-sdk:4.34.0") {
        exclude group: "com.android.support"
    }
    // Fresco
    implementation 'com.facebook.fresco:fresco:1.9.0'
    implementation 'com.facebook.fresco:animated-gif:1.9.0'

    // Helpshift
    implementation 'com.helpshift:android-helpshift-aar:7.1.0'

    // OKHttp3
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.9.0'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    // Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'

    // zxing
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.journeyapps:zxing-android-embedded:3.3.0@aar'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.0@aar') {
        transitive = true;
    }

    api("com.android.support:appcompat-v7:$support_lib_version") {
        exclude group: 'android.arch.core'
        exclude group: 'android.arch.lifecycle'
    }
    api("com.android.support:cardview-v7:$support_lib_version") {
        exclude group: 'android.arch.core'
        exclude group: 'android.arch.lifecycle'
    }
    api "com.android.support:design:$support_lib_version"
    implementation "android.arch.core:common:1.1.1"
    implementation "android.arch.lifecycle:runtime:1.1.1"
    implementation "android.arch.lifecycle:livedata-core:1.1.1"
    implementation "android.arch.lifecycle:viewmodel:1.1.1"

    implementation('com.timehop.stickyheadersrecyclerview:library:0.4.3') {
        exclude group: "com.android.support"
    }
    implementation 'com.aurelhubert:ahbottomnavigation:2.2.0'

    //Spinner
    //noinspection GradleDependency
    implementation 'com.jaredrummler:material-spinner:1.2.4'

    //material serach
    implementation('com.github.mancj:MaterialSearchBar:0.7.5') {
        exclude group: "com.android.support"
    }

    implementation 'com.romandanylyk:pageindicatorview:1.0.1'

    //Burred Backgrounds
    implementation 'jp.wasabeef:blurry:2.1.1'

    //EZXOPLAYER
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-dash:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayer_version"

    implementation 'com.loopj.android:android-async-http:1.4.9'

    // Taplytics
    implementation 'com.taplytics.sdk:taplytics:+@aar'
    //Braze
    implementation 'com.appboy:android-sdk-ui:2.6.0'


}

apply plugin: 'com.google.gms.google-services'
